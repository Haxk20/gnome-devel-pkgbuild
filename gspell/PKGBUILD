# $Id: PKGBUILD 264771 2016-04-12 21:46:30Z heftig $
# Maintainer: Jan de Groot <jgc@archlinux.org>
pkgname=gspell
pkgver=1.1.1
pkgrel=1
pkgdesc="a flexible API to implement the spell checking in a GTK+ application"
arch=('i686' 'x86_64')
url="https://wiki.gnome.org/Projects/gspell"
license=('GPL2')
depends=('gtk3' 'enchant' 'libxml2' 'iso-codes')
makedepends=('intltool' 'gobject-introspection' 'vala')
source=("https://download.gnome.org/sources/gspell/${pkgver:0:3}/gspell-${pkgver}.tar.xz")
sha256sums=('454edba601cf06088b0e75ac4dfae6450de9a82eb73bf20dd966cea5e389928d')

build() {
  cd "$pkgname-$pkgver"
  ./configure  --prefix=/usr
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}
package(){
  cd "$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
}
