# $Id: PKGBUILD 264771 2016-04-12 21:46:30Z heftig $
# Maintainer: Jan de Groot <jgc@archlinux.org>
pkgname=gspell
pkgver=1.2.0
pkgrel=1
pkgdesc="a flexible API to implement the spell checking in a GTK+ application"
arch=('i686' 'x86_64')
url="https://wiki.gnome.org/Projects/gspell"
license=('GPL2')
depends=('gtk3' 'enchant' 'libxml2' 'iso-codes')
makedepends=('intltool' 'gobject-introspection' 'vala')
source=("https://download.gnome.org/sources/gspell/${pkgver:0:3}/gspell-${pkgver}.tar.xz")
sha256sums=('6aa550b91e13c0073ec0dfef21b6eef730186912392e11128216b90519a3443f')

build() {
  cd "$pkgname-$pkgver"
  ./configure  --prefix=/usr
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}
package(){
  cd "$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
}
