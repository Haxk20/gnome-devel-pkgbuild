# $Id: PKGBUILD 264805 2016-04-12 22:30:00Z heftig $
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=yelp
pkgver=3.21.3
pkgrel=1
pkgdesc="A help browser for GNOME"
arch=('i686' 'x86_64')
depends=('webkit2gtk' 'yelp-xsl' 'libxslt' 'hicolor-icon-theme' 'xz' 'bzip2'
         'desktop-file-utils' 'dconf')
makedepends=('intltool' 'gtk-doc' 'itstool')
groups=('gnome')
license=('GPL')
options=('!emptydirs')
url="http://www.gnome.org"
install=yelp.install
source=(https://download.gnome.org/sources/$pkgname/${pkgver:0:4}/$pkgname-$pkgver.tar.xz)
sha256sums=('3ad727ab59b4c1fcf7526f60d8d360de4c6d169f3b5f68c935e95060f15114f5')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var --disable-static
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
