# $Id: PKGBUILD 264373 2016-04-09 13:19:37Z foutrelis $
# Maintainer: Jan de Groot  <jgc@archlinux.org>

pkgname=libgweather
pkgver=3.24.0
pkgrel=1
pkgdesc="Provides access to weather information from the net"
arch=('i686' 'x86_64')
license=('LGPL')
depends=('libsoup' 'gtk3' 'geocode-glib')
makedepends=('intltool' 'gobject-introspection' 'vala')
options=('!emptydirs')
url="http://www.gnome.org/"
install=libgweather.install
source=(https://download.gnome.org/sources/$pkgname/${pkgver:0:4}/$pkgname-$pkgver.tar.xz)
sha256sums=('1089b932205085add19e5e1b63361c0cff1a2c46a67ab2dded6dd2ca87bdfa3d')

build() {
    cd "$pkgname-$pkgver"
    ./configure --prefix=/usr --sysconfdir=/etc \
        --localstatedir=/var --disable-static
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
    make
}

package() {
    cd "$pkgname-$pkgver"
    make GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="$pkgdir" install
}
