# $Id: PKGBUILD 264333 2016-04-09 13:18:16Z foutrelis $
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=gnome-shell-extensions
pkgver=3.22.0
pkgrel=1
pkgdesc="Extensions for GNOME shell, including classic mode"
arch=(any)
url="https://wiki.gnome.org/Projects/GnomeShell/Extensions"
license=(GPL2)
depends=(gnome-shell)
makedepends=(intltool)
optdepends=('nautilus: gnome-classic session')
install=gnome-shell-extensions.install
groups=('gnome')
source=(http://ftp.gnome.org/pub/GNOME/sources/$pkgname/${pkgver:0:4}/$pkgname-$pkgver.tar.xz)
sha256sums=('317b35f6f1299d5162e693d39b21031f5fe875083c4020217db3ec056e245fc5')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --libexecdir=/usr/lib/gnome-session --enable-extensions=all
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
