# $Id: PKGBUILD 268322 2016-05-19 19:14:01Z heftig $
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
 
pkgname=graphene
pkgver=1.5.2
pkgrel=2
pkgdesc="A thin layer of graphic data types"
url="https://github.com/ebassi/graphene"
arch=(i686 x86_64)
license=(MIT)
depends=(glib2)
makedepends=(git gtk-doc gobject-introspection)
#source=("git+$url#tag=$pkgver")
source=("git+$url")
md5sums=('SKIP')

_confargs=
[[ $CARCH == i686 ]] && _confargs=--disable-sse2

prepare() {
  cd $pkgname

  git revert -n 217ac682ba4686267e05e9ff495afa5585f4b097

  NOCONFIGURE=1 ./autogen.sh
}
 
build() {
  cd $pkgname
  ./configure --prefix=/usr --enable-gtk-doc $_confargs
  make
}

check() {
  cd $pkgname
  make check
}
 
package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
