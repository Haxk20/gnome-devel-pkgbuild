# $Id: PKGBUILD 264850 2016-04-13 20:56:03Z heftig $
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=gnome-photos
pkgver=3.21.4
pkgrel=1
pkgdesc="Access, organize, and share your photos on GNOME"
arch=(i686 x86_64)
url="https://www.gnome.org/"
license=(GPL)
depends=(exempi gtk3 libexif librsvg babl gegl gnome-desktop gnome-online-accounts lcms2 tracker
         grilo gfbgraph libgdata gnome-online-miners dleyna-server dleyna-renderer libgexiv2 libraw)
makedepends=(intltool itstool python)
groups=(gnome-extra)
install=gnome-photos.install
source=(http://ftp.gnome.org/pub/GNOME/sources/${pkgname}/${pkgver:0:4}/${pkgname}-${pkgver}.tar.xz)
sha256sums=('cc5660109d3f3939a3bc6847028d42bee892944cf9d825276190db32e3710058')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var --libexecdir=/usr/lib/gnome-photos
  make
}

check() {
  cd $pkgname-$pkgver
# disabled: needs dogtail which we don't have
#  make check
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
