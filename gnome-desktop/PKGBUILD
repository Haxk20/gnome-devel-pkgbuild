# $Id: PKGBUILD 250515 2015-11-09 23:09:38Z heftig $
# Maintainer:  Jan de Groot <jan@archlinux.org>

pkgname=gnome-desktop
pkgver=3.19.90
pkgrel=1
epoch=1
pkgdesc="Library with common API for various GNOME modules"
arch=(i686 x86_64)
license=(GPL LGPL)
depends=(gsettings-desktop-schemas gtk3 libxkbfile xkeyboard-config iso-codes)
makedepends=(intltool gobject-introspection itstool)
url="http://www.gnome.org"
groups=(gnome)
source=(http://ftp.gnome.org/pub/gnome/sources/$pkgname/${pkgver:0:4}/$pkgname-$pkgver.tar.xz)
md5sums=('f5a06783be34b78baf2f26cbe85d664f')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
    --libexecdir=/usr/lib/$pkgname --disable-static \
    --with-gnome-distributor="Arch Linux"
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
