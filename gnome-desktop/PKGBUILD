# $Id: PKGBUILD 264664 2016-04-11 15:24:27Z heftig $
# Maintainer:  Jan de Groot <jan@archlinux.org>

pkgname=gnome-desktop
pkgver=3.22.0
pkgrel=1
epoch=1
pkgdesc="Library with common API for various GNOME modules"
arch=(i686 x86_64)
license=(GPL LGPL)
depends=(gsettings-desktop-schemas gtk3 libxkbfile xkeyboard-config iso-codes)
makedepends=(intltool gobject-introspection itstool)
url="http://www.gnome.org"
groups=(gnome)
source=(https://download.gnome.org/sources/$pkgname/${pkgver:0:4}/$pkgname-$pkgver.tar.xz)
sha256sums=('cff36ccd8d0a62177a4c1513ec70d13ead3b84fdc208ba54199cf7616f05644d')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
    --libexecdir=/usr/lib/$pkgname --disable-static \
    --with-gnome-distributor="Arch Linux"
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
