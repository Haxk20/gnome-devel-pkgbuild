# $Id: PKGBUILD 249323 2015-10-14 09:10:08Z heftig $
# Maintainer: Ionut Biru <ibiru@archlinux.org>

pkgbase=vte3
pkgname=(vte3 vte-common)
pkgver=0.43.92
pkgrel=1
pkgdesc="Virtual Terminal Emulator widget for use with GTK3"
arch=('i686' 'x86_64')
license=('LGPL')
options=('!emptydirs')
makedepends=('intltool' 'gobject-introspection' 'gtk3' 'vala' 'pcre2')
url="http://www.gnome.org"
source=("http://download.gnome.org/sources/vte/${pkgver::4}/vte-$pkgver.tar.xz")
md5sums=('993bd00b1bc55318336bf3c3197e3bed')

build() {
  cd "vte-$pkgver"
  ./configure --prefix=/usr --sysconfdir=/etc \
      --libexecdir=/usr/lib/vte \
      --localstatedir=/var --disable-static \
      --enable-introspection \
      --disable-test-application
  make
}

package_vte3(){
  depends=('gtk3' "vte-common=$pkgver")
  cd "vte-$pkgver"
  make DESTDIR="$pkgdir" install

  rm "$pkgdir"/etc/profile.d/vte.sh
}

package_vte-common() {
  pkgdesc="Common files used by vte and vte3"
  cd "vte-$pkgver"

  install -Dm644 src/vte.sh "$pkgdir"/etc/profile.d/vte.sh
}

