# $Id: PKGBUILD 264314 2016-04-09 13:17:32Z foutrelis $
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=gnome-documents
pkgver=3.23.91
pkgrel=2
pkgdesc="Documents Manager for GNOME"
arch=(i686 x86_64)
url="http://www.gnome.org"
license=(GPL)
groups=(gnome-extra)
depends=(evince gjs gtk3 gnome-desktop gnome-online-accounts libgdata
         tracker libzapojit webkit2gtk gnome-online-miners librsvg libgepub)
makedepends=(intltool gobject-introspection itstool docbook-xsl)
optdepends=('unoconv: Support for Libreoffice document types')
install=gnome-documents.install
source=(http://download.gnome.org/sources/$pkgname/${pkgver:0:4}/$pkgname-$pkgver.tar.xz)
sha256sums=('d7e1272701e02df2fffe80b0eb9702befee04481e0b33d5f61a4d7bbe3a50eb9')

build() {
  cd $pkgname-$pkgver
  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --libexecdir=/usr/lib/$pkgname
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}


check() {
  cd $pkgname-$pkgver
  make check
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
