# $Id: PKGBUILD 264296 2016-04-09 13:16:57Z foutrelis $
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>

pkgbase=glibmm
pkgname=('glibmm' 'glibmm-docs')
pkgver=2.50.0
pkgrel=2
arch=('i686' 'x86_64')
license=('LGPL')
makedepends=('glib2' 'libsigc++' 'pkgconfig')
depends=('perl')
source=(http://ftp.gnome.org/pub/GNOME/sources/$pkgname/${pkgver:0:4}/$pkgname-$pkgver.tar.xz)
url="http://www.gtkmm.org/"
sha256sums=('df726e3c6ef42b7621474b03b644a2e40ec4eef94a1c5a932c1e740a78f95e94')

build() {
  cd "$pkgname-$pkgver"
  ./configure --prefix=/usr
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

package_glibmm() {
  pkgdesc="Glib-- (glibmm) is a C++ interface for glib"
  depends=('glib2' 'libsigc++')
  cd "$pkgbase-$pkgver"
  sed -i -e 's/^doc_subdirs/#doc_subdirs/' Makefile
  make DESTDIR="$pkgdir" install
}

package_glibmm-docs() {
  pkgdesc="Developer documentation for glibmm"
  cd "$pkgbase-$pkgver/docs"
  make DESTDIR="$pkgdir" install
}
