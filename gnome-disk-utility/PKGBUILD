# $Id: PKGBUILD 264758 2016-04-12 21:38:30Z heftig $
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: Silvio Knizek (killermoehre)

pkgname=gnome-disk-utility
pkgver=3.21.91
pkgrel=1
pkgdesc="Disk Management Utility for GNOME"
arch=(i686 x86_64)
license=(GPL)
url="http://www.gnome.org"
groups=(gnome)
depends=(udisks2 gtk3 libsecret libpwquality libcanberra libdvdread libnotify desktop-file-utils parted)
makedepends=(intltool docbook-xsl gnome-settings-daemon)
options=('!emptydirs')
install=gnome-disk-utility.install
source=(http://ftp.gnome.org/pub/GNOME/sources/$pkgname/${pkgver:0:4}/$pkgname-$pkgver.tar.xz)
sha256sums=('6586275c08b0e6c08b5373fe5396ba8756bf7dcbcc32303e77a3a69499b773cf')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc --disable-static
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

check() {
  cd $pkgname-$pkgver
  make check
}

package() {
  cd "$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
}
