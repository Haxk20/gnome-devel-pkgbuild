# $Id: PKGBUILD 264339 2016-04-09 13:18:26Z foutrelis $
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=gnome-tetravex
pkgver=3.22.0
pkgrel=1
pkgdesc="Complete the puzzle by matching numbered tiles"
arch=(i686 x86_64)
license=(GPL)
depends=(hicolor-icon-theme gtk3)
makedepends=(intltool itstool gobject-introspection vala)
conflicts=(gnome-games)
replaces=(gnome-games)
options=(!emptydirs)
install=gnome-tetravex.install
url="https://live.gnome.org/Tetravex"
groups=(gnome-extra)
source=(https://download.gnome.org/sources/$pkgname/${pkgver:0:4}/$pkgname-$pkgver.tar.xz)
sha256sums=('0a6d7ff5ffcd6c05454a919d46a2e389d6b5f87bc80e82c52c2f20d9d914e18d')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
      --disable-schemas-compile
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
