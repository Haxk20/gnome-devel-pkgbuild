# $Id: PKGBUILD 264302 2016-04-09 13:17:10Z foutrelis $
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=gnome-calculator
pkgver=3.21.92
pkgrel=1
pkgdesc="GNOME Scientific calculator"
arch=('i686' 'x86_64')
license=('GPL')
depends=('gtk3' 'dconf' 'gtksourceview3' 'mpfr')
makedepends=('intltool' 'yelp-tools' 'vala')
groups=('gnome')
replaces=('gcalctool')
conflicts=('gcalctool')
options=(!emptydirs)
url="http://www.gnome.org"
install=gnome-calculator.install
source=(https://download.gnome.org/sources/${pkgname}/${pkgver:0:4}/${pkgname}-${pkgver}.tar.xz)
sha256sums=('9cbebb18c3db5179f8a690a5d661e7c7aeae9888b6d5a8a459e01f9c7f46ea9a')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc --disable-schemas-compile \
      --localstatedir=/var --libexecdir=/usr/lib/gnome-calculator
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="${pkgdir}" install
}
