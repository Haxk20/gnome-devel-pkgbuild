# $Id: PKGBUILD 264975 2016-04-14 21:47:58Z heftig $
# Maintainer: Ionut Biru <ibiru@archlinux.org>

pkgbase=gtkmm4
_pkgbasename=gtkmm
pkgname=('gtkmm4' 'gtkmm4-docs')
pkgver=3.22.0
pkgrel=1
arch=('i686' 'x86_64')
makedepends=('gtk4' 'pangomm' 'atkmm' 'glibmm-docs' 'mm-common' 'doxygen' 'libxslt')
license=('LGPL')
options=('!emptydirs')
url="http://www.gtkmm.org/"
source=(http://ftp.gnome.org/pub/GNOME/sources/$_pkgbasename/${pkgver:0:4}/$_pkgbasename-$pkgver.tar.xz)
sha256sums=('05da4d4b628fb20c8384630ddf478a3b5562952b2d6181fe28d58f6cbc0514f5')

build() {
  cd "$_pkgbasename-$pkgver"
  ./configure --prefix=/usr
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

package_gtkmm4() {
  pkgdesc="C++ bindings for gtk4"
  depends=('gtk4' 'pangomm' 'atkmm')
  cd "$_pkgbasename-$pkgver"

  sed -i -e 's/^doc_subdirs/#doc_subdirs/' Makefile

  make DESTDIR="$pkgdir" install
}

package_gtkmm4-docs() {
  pkgdesc="Developer documentation for gtkmm v4"

  cd "$_pkgbasename-$pkgver"
  make -C docs DESTDIR="$pkgdir" install
}
