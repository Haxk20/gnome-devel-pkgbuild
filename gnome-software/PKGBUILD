# $Id: PKGBUILD 146037 2015-11-05 13:37:22Z heftig $
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: Yosef Or Boczko <yoseforb@gnome.org>

pkgname=gnome-software
pkgver=3.21.1
pkgrel=6
pkgdesc="GNOME Software Tools"
arch=(i686 x86_64)
license=('GPL2')
url="https://wiki.gnome.org/Apps/Software/"
depends=(gtk3 appstream-glib libsoup gsettings-desktop-schemas gnome-desktop packagekit
         desktop-file-utils archlinux-appstream-data xdg-app)
makedepends=(intltool)
install=gnome-software.install
source=(http://download.gnome.org/sources/$pkgname/${pkgver:0:4}/$pkgname-$pkgver.tar.xz
        fix-css.patch::https://git.gnome.org/browse/gnome-software/patch/?id=83b46a2a063ee1d4991779364f57bac07413f9ad)
sha256sums=('c903dac3c4a7e3e10aed6b8d72302f96319ea27f71e6d055c9db6eae73cd2301'
            '32381db0074532784785dacc6855377a2bf4ccdb67c09a6164b910d3f68cfef4')

prepare () {
  cd $pkgname-$pkgver

  patch -Np1 -i ../fix-css.patch
}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
    --disable-static --disable-schemas-compile
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="${pkgdir}" install

  # Remove fedora-only plugins
  rm "${pkgdir}"/usr/lib/gs-plugins-*/libgs_plugin_fedora_tagger*
}
