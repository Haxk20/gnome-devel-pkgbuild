# $Id: PKGBUILD 250095 2015-11-04 08:57:06Z jgc $
# Maintainer: Ionut Biru <ibiru@archlinux.org>

pkgname=sushi
pkgver=3.19.90
pkgrel=1
pkgdesc="A quick previewer for Nautilus"
arch=(i686 x86_64)
url="http://www.gnome.org"
license=('GPL2')
groups=(gnome)
depends=('clutter-gtk' 'clutter-gst' 'gjs' 'libmusicbrainz5' 'webkit2gtk' 'gtksourceview3' 'evince')
optdepends=('unoconv: OpenDocument formats')
install=sushi.install
makedepends=('intltool' 'gobject-introspection')
source=(http://download.gnome.org/sources/$pkgname/${pkgver:0:4}/$pkgname-$pkgver.tar.xz)
sha256sums=('040aeec75397c1920ba7982395d5f3f8c732e17778c47df2b97f1c4ad06ceb69')

prepare() {
  cd "$pkgname-$pkgver"
}

build() {
  cd "$pkgname-$pkgver"
  autoreconf -fi
  ./configure --prefix=/usr --libexecdir=/usr/lib/sushi
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0 /g' -e 's/    if test "$export_dynamic" = yes && test -n "$export_dynamic_flag_spec"; then/      func_append compile_command " -Wl,-O1,--as-needed"\n      func_append finalize_command " -Wl,-O1,--as-needed"\n\0/' libtool
  make
}

package() {
  cd "$pkgname-$pkgver"
  make DESTDIR="$pkgdir/" install
}

# vim:set ts=2 sw=2 et:
