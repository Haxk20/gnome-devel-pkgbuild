# $Id: PKGBUILD 264802 2016-04-12 22:27:39Z heftig $
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=gnome-maps
pkgver=3.22.0
pkgrel=1
pkgdesc="A simple GNOME 3 maps application"
arch=(i686 x86_64)
license=('GPL')
url="https://live.gnome.org/Maps"
depends=(geocode-glib geoclue2 gjs libchamplain gnome-themes-standard libgee folks gfbgraph)
makedepends=(intltool gobject-introspection)
groups=('gnome-extra')
install=gnome-maps.install
source=(https://download.gnome.org/sources/$pkgname/${pkgver:0:4}/$pkgname-$pkgver.tar.xz)
sha256sums=('7ce98a683f1c38d3ba1b5d68c7d05add9f9366774678fd50fdeeda6987163c39')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --disable-schemas-compile
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

package() {
  cd $pkgname-$pkgver
  make -j1 DESTDIR="${pkgdir}" install
}
