# $Id: PKGBUILD 264277 2016-04-09 13:16:20Z foutrelis $
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=evince
pkgver=3.21.92
pkgrel=1
pkgdesc="Document viewer (PDF, Postscript, djvu, tiff, dvi, XPS, SyncTex support with gedit, comics books (cbr,cbz,cb7 and cbt))"
url="https://wiki.gnome.org/Apps/Evince"
arch=(i686 x86_64)
license=(GPL)
depends=(gtk3 libgxps libspectre gsfonts poppler-glib djvulibre t1lib libsecret desktop-file-utils dconf gsettings-desktop-schemas gnome-desktop libarchive)
makedepends=(itstool libnautilus-extension texlive-bin intltool gobject-introspection docbook-xsl python)
optdepends=('texlive-bin: DVI support'
	    'gvfs: bookmark support and session saving')
groups=(gnome)
install=evince.install
options=('!emptydirs')
source=(https://download.gnome.org/sources/$pkgname/${pkgver:0:4}/$pkgname-$pkgver.tar.xz)
sha256sums=('88b41507545a2dc12712af0574a2f32cce1551467f997f3485f0a7cca1f8ff16')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr   --sysconfdir=/etc \
      --localstatedir=/var    --libexecdir=/usr/lib/evince \
      --disable-static        --enable-nautilus \
      --enable-pdf            --enable-tiff \
      --enable-djvu           --enable-dvi \
      --enable-t1lib          --enable-comics \
      --disable-schemas-compile \
      --enable-introspection --disable-browser-plugin
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
