# $Id: PKGBUILD 264307 2016-04-09 13:17:19Z foutrelis $
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=gnome-color-manager
pkgver=3.21.90
pkgrel=1
pkgdesc="Color profile manager for the GNOME desktop"
arch=(i686 x86_64)
url="http://projects.gnome.org/gnome-color-manager/"
license=(GPL2)
depends=(libcanberra vte3 exiv2 desktop-file-utils shared-color-targets colord-gtk libexif)
makedepends=(intltool yelp-tools)
optdepends=('argyllcms: color calibration')
install=gnome-color-manager.install
options=('!emptydirs')
groups=('gnome-extra')
source=(http://ftp.gnome.org/pub/GNOME/sources/$pkgname/${pkgver:0:4}/$pkgname-$pkgver.tar.xz
	"fix-man-pages-to-use-the-correct-package-name.patch::https://git.gnome.org/browse/gnome-color-manager/patch/?id=3c8fb5c8d222544ac98341b9a0fd0999ef7837dd")
sha256sums=('3ad59d078d9624086f04effd0af7d830e09ffa736fa87eca7ff0015400299de8')

prepare(){
  cd $pkgname-$pkgver
  
  patch -p1 -i ../fix-man-pages-to-use-the-correct-package-name.patch
}

build(){
  cd $pkgname-$pkgver

  # man pages need docbook-sgml, which we don't have
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
      --libexecdir=/usr/lib/gnome-color-manager --disable-static --disable-man-pages

  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
