# $Id: PKGBUILD 266461 2016-04-28 13:48:54Z jgc $
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=gnome-builder
pkgver=3.24.0
pkgrel=1
pkgdesc="An IDE for writing GNOME-based software"
url="https://wiki.gnome.org/Apps/Builder"
arch=(i686 x86_64)
license=(GPL3)
depends=('gtksourceview3' 'devhelp' 'libgit2-glib' 'gjs' 'python-gobject' 'clang'
	 'desktop-file-utils' 'ctags' 'libpeas' 'vte3' 'vala' 'python-jedi'
	 'flatpak' 'mm-common' 'sysprof' 'gtkmm3' 'git' 'gspell' 'meson')
makedepends=(intltool llvm gobject-introspection gtk-doc)
groups=(gnome-extra)
source=(http://download.gnome.org/sources/$pkgname/${pkgver:0:4}/$pkgname-$pkgver.tar.xz)
sha256sums=('3c2948f451dd107b3838bd6580bd7df95aa455d16f595aefab6e7553cccfc8d2')


build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
    --libexecdir=/usr/lib --enable-gtk-doc
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

check() {
  cd $pkgname-$pkgver
  make -k check || :
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
