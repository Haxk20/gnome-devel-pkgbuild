# $Id: PKGBUILD 264384 2016-04-09 13:20:00Z foutrelis $
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=polari
pkgver=3.23.1
pkgrel=1
pkgdesc="An IRC Client for GNOME"
arch=(i686 x86_64)
license=(GPL)
url="https://wiki.gnome.org/Apps/Polari"
depends=(gjs gtk3 telepathy-glib telepathy-idle telepathy-mission-control telepathy-logger)
makedepends=(intltool gobject-introspection desktop-file-utils appdata-tools)
groups=(gnome-extra)
install=polari.install
source=(https://download.gnome.org/sources/$pkgname/${pkgver:0:4}/$pkgname-$pkgver.tar.xz)
sha256sums=('bac23878da5f65232b58897e97924678b107324eec7449233a5cbe469919dff7')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
