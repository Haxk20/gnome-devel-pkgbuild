# $Id: PKGBUILD 264372 2016-04-09 13:19:34Z foutrelis $
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=libgtop
pkgver=2.35.90
pkgrel=1
pkgdesc="A library that reads information about processes and the running system"
arch=('i686' 'x86_64')
license=('LGPL')
depends=('glib2' 'libxau' 'texinfo')
makedepends=('intltool' 'gobject-introspection')
install=libgtop.install
source=(https://download.gnome.org/sources/${pkgname}/${pkgver:0:4}/${pkgname}-${pkgver}.tar.xz)
url="http://www.gnome.org/"
sha256sums=('e98badc3bdbebbfaeddb633fae908fac8cc62493183ada5614d856af9a755bfe')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var --disable-static \
      --with-libgtop-smp
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
