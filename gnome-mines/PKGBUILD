# $Id: PKGBUILD 250507 2015-11-09 22:57:12Z heftig $
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=gnome-mines
pkgver=3.19.90
pkgrel=1
pkgdesc="Clear hidden mines from a minefield"
arch=('i686' 'x86_64')
license=('GPL')
depends=('hicolor-icon-theme' 'gtk3' 'librsvg')
makedepends=('intltool' 'itstool' 'gobject-introspection')
conflicts=('gnome-games')
replaces=('gnome-games')
options=('!emptydirs')
install=gnome-mines.install
url="https://wiki.gnome.org/Apps/Mines"
groups=('gnome-extra')
source=(http://ftp.gnome.org/pub/gnome/sources/$pkgname/${pkgver:0:4}/$pkgname-$pkgver.tar.xz)
md5sums=('2f84a7c925bdfe84a31db1d3f080dabd')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
      --disable-schemas-compile
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
