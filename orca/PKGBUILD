# $Id: PKGBUILD 266584 2016-04-28 20:19:52Z jgc $
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: William Rea <sillywilly@gmail.com>

pkgname=orca
pkgver=3.21.1
pkgrel=1
pkgdesc="Screen reader for individuals who are blind or visually impaired"
arch=(any)
license=('LGPL')
url="http://www.gnome.org/projects/orca"
depends=(gtk3 at-spi2-atk python-atspi python-dbus python-xdg speech-dispatcher liblouis brltty
         xorg-xmodmap hicolor-icon-theme gsettings-desktop-schemas
         gst-plugins-base # playbin, audiotestsrc, basic decoders
         gst-plugins-good # pulsesink, more decoders
)
makedepends=(itstool intltool)
groups=(gnome-extra)
source=(http://ftp.gnome.org/pub/GNOME/sources/$pkgname/${pkgver:0:4}/$pkgname-$pkgver.tar.xz)
sha256sums=('68e67d764a222c5ec186b52dcffe1ab9599a9cc94b8dc94fa5d3b088afae1d13')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
