# $Id: PKGBUILD 264283 2016-04-09 13:16:30Z foutrelis $
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=five-or-more
pkgver=3.21.91
pkgrel=1
pkgdesc="Remove colored balls from the board by forming lines"
arch=('i686' 'x86_64')
license=('GPL')
depends=('hicolor-icon-theme' 'gtk3' 'librsvg')
makedepends=('intltool' 'itstool' 'gobject-introspection')
conflicts=('gnome-games')
replaces=('gnome-games')
options=('!emptydirs')
install=five-or-more.install
url="https://wiki.gnome.org/Apps/Five%20or%20more"
groups=('gnome-extra')
source=(https://download.gnome.org/sources/$pkgname/${pkgver:0:4}/$pkgname-$pkgver.tar.xz)
sha256sums=('780c8f3a7bc64cb7e3afc0e5302b83aaf27d283fbe92d3ff2bcc2156700a317d')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
      --disable-schemas-compile
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
