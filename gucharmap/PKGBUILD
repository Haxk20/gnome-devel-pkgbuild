# $Id: PKGBUILD 264360 2016-04-09 13:19:11Z foutrelis $
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Jan De Groot <jgc@archlinux.org>

pkgname=gucharmap
pkgver=9.0.1
pkgrel=1
pkgdesc="Gnome Unicode Charmap"
arch=(i686 x86_64)
license=(GPL)
url="http://www.gnome.org"
depends=(dconf gtk3 hicolor-icon-theme)
makedepends=(gtk-doc intltool gobject-introspection itstool docbook-xsl desktop-file-utils python2
             appdata-tools unicode-character-database unzip)
options=(!emptydirs)
groups=(gnome)
install=gucharmap.install
source=(https://download.gnome.org/sources/$pkgname/${pkgver:0:3}/$pkgname-$pkgver.tar.xz)
sha256sums=('8617ab68f2977cc3780a1f39264a48b8d9cf8824172c442fa9ca1c11d81dbd6c')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var --disable-static \
      --disable-schemas-compile  --enable-introspection \
      --with-unicode-data=/usr/share/unicode-character-database
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
