# $Id: PKGBUILD 264764 2016-04-12 21:42:47Z heftig $
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=libgnome-games-support
pkgver=1.1.91
pkgrel=1
pkgdesc="Code shared between GNOME games"
url="https://git.gnome.org/browse/libgames-support/"
license=(LGPL)
arch=(i686 x86_64)
replaces=(libgames-support)
provides=(libgames-support)
depends=(gtk3 libgee)
makedepends=(gobject-introspection vala intltool)
source=(http://ftp.gnome.org/pub/GNOME/sources/$pkgname/${pkgver:0:3}/$pkgname-$pkgver.tar.xz)
sha256sums=('d894c1526bbda095bec148a8affc9f9a924a0f817d91f38ffccee659fd4051f3')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --disable-static
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
