# $Id: PKGBUILD 264294 2016-04-09 13:16:53Z foutrelis $
# Maintainer: Ionut Biru <ibiru@archlinux.org>

pkgname=gjs
pkgver=1.47.4
pkgrel=1
pkgdesc="Javascript Bindings for GNOME"
arch=(i686 x86_64)
url="http://live.gnome.org/Gjs"
license=(GPL)
depends=(cairo gobject-introspection-runtime 'js38' 'gtk3')
makedepends=('gobject-introspection')
source=(https://download.gnome.org/sources/gjs/${pkgver:0:4}/$pkgname-$pkgver.tar.xz)
sha256sums=('8189d8277c08f5736213811ccf3fe4d0fe70be47b4b3c5af4b968f02c7f4a917')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --disable-static --libexecdir=/usr/lib
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

check() {
  cd $pkgname-$pkgver
  # Needs a display
  make -k check || :
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
