# $Id: PKGBUILD 263475 2016-03-30 22:30:08Z heftig $
# Maintainer: Roman Kyrylych <roman@archlinux.org>

pkgname=gnome-user-share
pkgver=3.18.3
pkgrel=1
pkgdesc="Easy to use user-level file sharing for GNOME."
arch=('i686' 'x86_64')
url="http://ftp.gnome.org/pub/GNOME/sources/gnome-user-share/"
license=('GPL')
depends=('mod_dnssd' 'dconf' 'gtk3' 'hicolor-icon-theme' 'libnotify' 'libcanberra')
makedepends=('intltool' 'libnautilus-extension' 'itstool' 'docbook-xsl')
options=('!emptydirs')
groups=('gnome')
install=gnome-user-share.install
source=(http://ftp.gnome.org/pub/GNOME/sources/${pkgname}/${pkgver:0:4}/${pkgname}-${pkgver}.tar.xz)
sha256sums=('3092fa7ad137531e35484195dde4ecbbc75a8a3d9b8209aef4c852717cf4bbf1')

build() {
  cd ${pkgname}-${pkgver}
  ./configure --prefix=/usr --sysconfdir=/etc \
      --libexecdir=/usr/lib/gnome-user-share \
      --localstatedir=/var
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
}
