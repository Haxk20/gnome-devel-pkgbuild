# $Id: PKGBUILD 264323 2016-04-09 13:17:52Z foutrelis $
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=gnome-music
pkgver=3.23.3
pkgrel=1
pkgdesc="Music player and management application"
arch=(i686 x86_64)
license=('GPL')
url="https://wiki.gnome.org/Apps/Music"
depends=(grilo grilo-plugins tracker libdmapsharing python-dbus dbus-glib gnome-themes-standard gtk3 gvfs python-gobject python-cairo gst-plugins-base python-requests)
makedepends=(intltool itstool gobject-introspection)
optdepends=('gst-plugins-good: Extra media codecs'
            'gst-plugins-ugly: Extra media codecs'
            'gst-plugins-bad: Extra media codecs'
            'gst-libav: Extra media codecs')
groups=('gnome-extra')
install=gnome-music.install
source=(https://download.gnome.org/sources/$pkgname/${pkgver:0:4}/$pkgname-$pkgver.tar.xz)
sha256sums=('54f0bb3375345d210d0287ea6d1d345966a08f16e63115a7a1acd3040738af49')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --disable-schemas-compile
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

package() {
  cd $pkgname-$pkgver
  make -j1 DESTDIR="${pkgdir}" install
}
